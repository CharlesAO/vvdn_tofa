/*  This is a Leon Default Linker script 
 *  Should be used as an included linker script in top level scripts 
 *
 *  Addresses defined in common/drivers/myriad2/system/asm/memmap.S
 */
/*  linkcmds
 */
 
INCLUDE myriad2_arch_defaults.ldscript

 SECTIONS
{
  /* 
   * locate the special sections in correct place  
   * These sections are required to ensure Myriad is initialized correctly and must be loaded first   
   */   
  .l2.mode    0x20FD0000 : { KEEP(*(.l2.mode)) }           /* L2 cache mode                */
  .cmx.ctrl   0x20FC0028 : { KEEP(*(.cmx.ctrl)) }          /* CMX default layout           */
  
  /* CMX DMA descriptors need to stay in slice 12 (16 for ma2x8x)*/
  . = _RAM_START_LOS_DEFAULT;
  . = . | 0x08000000;
  _CDMA_DESCRIPTORS_START = .;
  .cmx.cdmaDescriptors : AT(ABSOLUTE(_CDMA_DESCRIPTORS_START) - 0x08000000)
  { 
      *(.cmx.cdmaDescriptors*) *(*.cmx.cdmaDescriptors*)
  }
  _CDMA_DESCRIPTORS_END = .;

  _CDMA_DESCRIPTORS_SIZE = _CDMA_DESCRIPTORS_END - _CDMA_DESCRIPTORS_START;
  . = _CDMA_DESCRIPTORS_END;
  . = ALIGN (4);

  __cmx_resmgr_start = .;
  .cmx.resmgr ALIGN(8) :
  {
      *(.cmx.resmgr*) *(*.cmx.resmgr*)
  }
  __cmx_resmgr_end = .;

  . = __cmx_resmgr_end - 0x08000000;
  __NEXT_FREE_CMX = .;
  _RESMGR_SIZE = __cmx_resmgr_end - __cmx_resmgr_start;
  _SPECIAL_SECTIONS_SIZE = _CDMA_DESCRIPTORS_SIZE + _RESMGR_SIZE;
  
 }

/*
* Settings for LOS 
*/
_PROM_SIZE_LOS = DEFINED (_PROM_SIZE_LOS) ? _PROM_SIZE_LOS : 1024;
_PROM_START_LOS = DEFINED (_PROM_START_LOS) ? _PROM_START_LOS : 0x00000000;

/*_RAM_SIZE_LOS = DEFINED (_RAM_SIZE_LOS) ? _RAM_SIZE_LOS : 524288;*/
_RAM_SIZE_LOS = DEFINED (_RAM_SIZE_LOS) ? _RAM_SIZE_LOS : (262144 - _SPECIAL_SECTIONS_SIZE);
_RAM_START_LOS = DEFINED (_RAM_START_LOS) ? (_RAM_START_LOS == _RAM_START_LOS_DEFAULT) ? _RAM_START_LOS +  _SPECIAL_SECTIONS_SIZE : _RAM_START_LOS : __NEXT_FREE_CMX;

/* these are the maximum values */
__bss_end = _end;
__sys_bss_start = _RAM_START_LOS + _RAM_SIZE_LOS - 0x200;
__sys_bss_end = _RAM_START_LOS + _RAM_SIZE_LOS;

/*
* Settings for LRT 
*/
_PROM_SIZE_LRT = DEFINED (_PROM_SIZE_LRT) ? _PROM_SIZE_LRT : 1024;
_PROM_START_LRT = DEFINED (_PROM_START_LRT) ? _PROM_START_LRT : 0x00000000;

/*_RAM_SIZE_LRT = DEFINED (_RAM_SIZE_LRT) ? _RAM_SIZE_LRT : 262144;*/
/*150 KB for LeonRT by default. 100KB will be left then for general code/data.*/
_RAM_SIZE_LRT = DEFINED (_RAM_SIZE_LRT) ? _RAM_SIZE_LRT : 153600;
_RAM_START_LRT = DEFINED (_RAM_START_LRT) ? _RAM_START_LRT : _RAM_START_LRT_DEFAULT;

/* these are the maximum values */
lrt___bss_end = lrt__end;
lrt___sys_bss_start = _RAM_START_LRT + _RAM_SIZE_LRT - 0x200;
lrt___sys_bss_end = _RAM_START_LRT + _RAM_SIZE_LRT;

/*  linkcmds
 */

__DYNAMIC  =  0;
ENTRY(start)

/*
 * The memory map looks like this:
 * +--------------------+ <- low memory
 * | .text              |
 * |        etext       |
 * |        ctor list   | the ctor and dtor lists are for
 * |        dtor list   | C++ support
 * |        _endtext    |
 * +--------------------+
 * | .data              | initialized data goes here
 * |        _sdata      |
 * |        _edata      |
 * +--------------------+
 * | .bss               |
 * |        __bss_start | start of bss, cleared by crt0
 * |        _end        | start of heap, used by sbrk()
 * +--------------------+
 * |    heap space      |
 * |        _ENDHEAP    |
 * |    stack space     | 
 * |        __stack     | top of stack
 * +--------------------+ <- high memory
 */

_RAM_END_LOS = _RAM_START_LOS + _RAM_SIZE_LOS;

_PROM_END_LOS = _PROM_START_LOS + _PROM_SIZE_LOS;

/*
 *  Alternate names without leading _.
 */

PROM_START_LOS = _PROM_START_LOS;
PROM_SIZE_LOS = _PROM_SIZE_LOS;
PROM_END_LOS = _PROM_END_LOS;

RAM_START_LOS = _RAM_START_LOS;
RAM_SIZE_LOS = _RAM_SIZE_LOS;
RAM_END_LOS = _RAM_END_LOS;

/*
 * SPARC monitor assumes this is present to provide proper RTEMS awareness.
 */
EXTERN(rtems_get_version_string);

/*
 * stick everything in ram (of course)
 */

SECTIONS
{
  . = ABSOLUTE(RAM_START_LOS);
  .text ABSOLUTE(RAM_START_LOS) : AT(ABSOLUTE(RAM_START_LOS))
  {
    CREATE_OBJECT_SYMBOLS
    text_start = .;
    _text_start = .;	
	/********************************************/
	/* In order to include the trap table first */	
    . = ALIGN(0x1000);
      *(.sys.text.traps)
    . = ALIGN(4);
      *(.sys.text.start)
      *(.sys.text*)
	/********************************************/
	. = ALIGN (4);
    *(.text*)
    . = ALIGN (16);

    KEEP(*(.eh_frame*))
    . = ALIGN (16);

    *(.gnu.linkonce.t*)
    }
  .ctors : 
  {
    /*
     * C++ constructors
     */
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */
    CTORS_BEGIN = .;
    KEEP (*crtbegin.o(.ctors))
    KEEP (*crtbegin?.o(.ctors))
    /* We don't want to include the .ctor section from
       the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .ctors))
    KEEP (*(SORT(.ctors*)))
    CTORS_END = .;
  }
    
  .dtors : 
  {
    KEEP (*crtbegin.o(.dtors))
    KEEP (*crtbegin?.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .dtors))
    KEEP (*(SORT(.dtors*)))
  }
	
  .rodata : 
  {
	. = ALIGN(4);
    _rodata_start = . ;
	/* Adding startup readonly data */
	*(.sys.rodata*)
    *(.rodata*)
    *(.gnu.linkonce.r*)
    _erodata = ALIGN( 0x10 ) ;
    etext = ALIGN(0x10);
    _etext = .;
  }  
  .init : 
  {
    KEEP(*(.init*))
  }
  .fini : 
  {
    KEEP(*(.fini*))
    *(.lit)
    *(.shdata)
    . = ALIGN (16);
  }  
  .tdata : {
    _TLS_Data_begin = .;
    *(.tdata .tdata.* .gnu.linkonce.td.*)
    _TLS_Data_end = .;
  }  
  .tbss : {
    _TLS_BSS_begin = .;
    *(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon)
    _TLS_BSS_end = .;
  }  
  _TLS_Data_size = _TLS_Data_end - _TLS_Data_begin;
  _TLS_BSS_size = _TLS_BSS_end - _TLS_BSS_begin;
  _TLS_Size = _TLS_BSS_end - _TLS_Data_begin;
  _TLS_Alignment = ALIGNOF (.tdata);
  .rtemsroset : {
    /* for pre rtems-libbsd FreeBSD code */
    __start_set_sysctl_set = .;
    KEEP(*(set_sysctl_*));
    __stop_set_sysctl_set = .;
    KEEP(*(set_domain_*));
    KEEP(*(set_pseudo_*));

    KEEP (*(SORT(.rtemsroset.*)))

    . = ALIGN (16);
    _endtext = .;
  }  
  .ARM.attributes : 
  {
    *(.ARM.attributes*)
  }
  .ARM.exidx :
  {
    *(.ARM.exidx*)
  }
  .rela.dyn       :
    {
      *(.rela.init)
      *(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
      *(.rela.fini)
      *(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
      *(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
      *(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
      *(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
      *(.rela.ctors)
      *(.rela.dtors)
      *(.rela.got)
      *(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
    }  
  .rtemsrwset : {
    KEEP (*(SORT(.rtemsrwset.*)))
  }  
  .data :
  {
    data_start = .;
    _data_start = .;
    _sdata = . ;
    *(.data*)
    *(.gnu.linkonce.d*)
    . = ALIGN(0x10);
    *(.gcc_except_table*)
    . = ALIGN(0x10);
    edata = .;
    _edata = .;
  }  
  
  /*The LeonOS default heap section*/
  .heapSection : { *(.heapSection.data) }
  
  .dynamic	  : { *(.dynamic) 	}  
  .jcr		  : { *(.jcr) 		}  
  .got		  : { *(.got) 		}  
  .plt   	  : { *(.plt) 		}  
  .hash		  : { *(.hash)		}  
  .dynrel	  : { *(.dynrel)	}  
  .dynsym	  : { *(.dynsym)	}  
  .dynstr	  : { *(.dynstr)	}  
  .hash		  : { *(.hash)		}  
  .shbss :
  {
    *(.shbss)
  }  
  
  .bss :
  {
    __bss_start = ALIGN(0x8);
    _bss_start = .;
    bss_start = .;
    *(.bss .bss* .gnu.linkonce.b*)
    *(COMMON)
    . = ALIGN (16);
    end = .;
    _end = ALIGN(0x8);
    __end = ALIGN(0x8);
  } 
  
  .noinit (NOLOAD) : { 
    . = ALIGN(8);
    __noinit_start = . ;
    *(.noinit*) *(COMMON)
    . = ALIGN(8);
    __noinit_end = . ;
  }
  
  /*Move section pointer to the end of the allocated memory*/
  . = __sys_bss_start;
  _LEON_STACK_POINTER = ALIGN(8);
  .sys.bss ALIGN(8) (NOLOAD) : { 
    . = ALIGN(8);
    __sys_bss_start = . ;
    *(.sys.bss*) 
    . = ALIGN(8);
    __sys_bss_end = . ;
  }
  
  .stab . (NOLOAD) :
  {
    [ .stab ]
  }
  .stabstr . (NOLOAD) :
  {
    [ .stabstr ]
  }  
 }
/*----------------------------------------------------------------*/
/*------------Same sections follow for LeonRT---------------------*/
/*----------------------------------------------------------------*
lrt___DYNAMIC  =  0;

/*
 * The memory map looks like this:
 * +------------------------+ <- low memory
 * | .lrt.text              |
 * |        lrt_etext       |
 * |        ctor list       | the ctor and dtor lists are for
 * |        dtor list       | C++ support
 * |        lrt__endtext    |
 * +------------------------+
 * | .lrt.data              | initialized data goes here
 * |        lrt__sdata      |
 * |        lrt__edata      |
 * +------------------------+
 * | .lrt.bss               |
 * |        lrt___bss_start | start of bss, cleared by crt0
 * |        lrt__end        | start of heap, used by sbrk()
 * +------------------------+   
 * |    heap space          |
 * |        lrt__ENDHEAP    |
 * |    stack space         | 
 * |        lrt__stack      | top of stack
 * +------------------------+ <- high memory
 */

_RAM_END_LRT = _RAM_START_LRT + _RAM_SIZE_LRT;

_PROM_END_LRT = _PROM_START_LRT + _PROM_SIZE_LRT;

/*
 *  Alternate names without leading _.
 */

PROM_START_LRT = _PROM_START_LRT;
PROM_SIZE_LRT = _PROM_SIZE_LRT;
PROM_END_LRT = _PROM_END_LRT;

RAM_START_LRT = _RAM_START_LRT;
RAM_SIZE_LRT = _RAM_SIZE_LRT;
RAM_END_LRT = _RAM_END_LRT;

/*
 * SPARC monitor assumes this is present to provide proper RTEMS awareness.
 */
EXTERN(lrt_rtems_get_version_string);

/*
 * stick everything in ram (of course)
 */
SECTIONS
{
  . = ABSOLUTE(RAM_START_LRT);
  .lrt.text ABSOLUTE(RAM_START_LRT) : AT(ABSOLUTE(RAM_START_LRT))
  {
    CREATE_OBJECT_SYMBOLS
    lrt_text_start = .;
    lrt__text_start = .;	
	/********************************************/
	/* In order to include the trap table first */	
	. = ALIGN(0x1000);
      *(.lrt.sys.text.traps)
  /* LeonRT Entry point has a requirement that the bottom 12 bits
     are 0. Thus 4KB alignment is required */
    . = ALIGN(0x1000);
      *(.lrt.sys.text.start)
      *(.lrt.sys.text*)
	/********************************************/
	. = ALIGN (4);
    *(.lrt.text*)
    . = ALIGN (16);

    KEEP(*(.lrt.eh_frame*))
    . = ALIGN (16);

    *(.lrt.gnu.linkonce.t*)
  }
  .lrt.ctors : 
  {
    /*
     * C++ constructors
     */
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */
    lrt_CTORS_BEGIN = .;
    KEEP (*crtbegin.o(.lrt.ctors))
    KEEP (*crtbegin?.o(.lrt.ctors))
    /* We don't want to include the .ctor section from
       the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .lrt.ctors))
    KEEP (*(SORT(.lrt.ctors*)))
    lrt_CTORS_END = .;
  }
  .lrt.dtors : 
  {
    KEEP (*crtbegin.o(.lrt.dtors))
    KEEP (*crtbegin?.o(.lrt.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .lrt.dtors))
    KEEP (*(SORT(.lrt.dtors*)))
  }
  .lrt.rodata : 
  {
	. = ALIGN(4);
    lrt__rodata_start = . ;
	/* Adding startup readonly data */
	*(.lrt.sys.rodata*)
    *(.lrt.rodata*)
    *(.lrt.gnu.linkonce.r*)
    lrt__erodata = ALIGN( 0x10 ) ;

    lrt_etext = ALIGN(0x10);
    lrt__etext = .;
  }
  .lrt.init : 
  {
    KEEP(*(.lrt.init*))
  }
  .lrt.fini : 
  {
    KEEP(*(.lrt.fini*))
    *(.lrt.lit)
    *(.lrt.shdata)
    . = ALIGN (16);
  }  
  .lrt.tdata : {
    lrt__TLS_Data_begin = .;
    *(.lrt.tdata .lrt.tdata.* .lrt.gnu.linkonce.td.*)
    lrt__TLS_Data_end = .;
  }  
  .lrt.tbss : {
    lrt__TLS_BSS_begin = .;
    *(.lrt.tbss .lrt.tbss.* .lrt.gnu.linkonce.tb.*) *(.lrt.tcommon)
    lrt__TLS_BSS_end = .;
  }  
  lrt__TLS_Data_size = lrt__TLS_Data_end - lrt__TLS_Data_begin;
  lrt__TLS_BSS_size = lrt__TLS_BSS_end - lrt__TLS_BSS_begin;
  lrt__TLS_Size = lrt__TLS_BSS_end - lrt__TLS_Data_begin;
  lrt__TLS_Alignment = ALIGNOF (.lrt.tdata);
  .lrt.rtemsroset : {
    /* for pre rtems-libbsd FreeBSD code */
    lrt___start_set_sysctl_set = .;
    KEEP(*(lrt_set_sysctl_*));
    lrt___stop_set_sysctl_set = .;
    KEEP(*(lrt_set_domain_*));
    KEEP(*(lrt_set_pseudo_*));

    KEEP (*(SORT(.lrt.rtemsroset.*)))

    . = ALIGN (16);
    lrt__endtext = .;
  }  
  .lrt.ARM.attributes : 
  {
    *(.lrt.ARM.attributes*)
  }
  .lrt.ARM.exidx :
  {
    *(.lrt.ARM.exidx*)
  }
  .lrt.rela.dyn       :
    {
      *(.lrt.rela.init)
      *(.lrt.rela.text .lrt.rela.text.* .lrt.rela.gnu.linkonce.t.*)
      *(.lrt.rela.fini)
      *(.lrt.rela.rodata .lrt.rela.rodata.* .lrt.rela.gnu.linkonce.r.*)
      *(.lrt.rela.data .lrt.rela.data.* .lrt.rela.gnu.linkonce.d.*)
      *(.lrt.rela.tdata .lrt.rela.tdata.* .lrt.rela.gnu.linkonce.td.*)
      *(.lrt.rela.tbss .lrt.rela.tbss.* .lrt.rela.gnu.linkonce.tb.*)
      *(.lrt.rela.ctors)
      *(.lrt.rela.dtors)
      *(.lrt.rela.got)
      *(.lrt.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
    }  
  .lrt.rtemsrwset : {
    KEEP (*(SORT(.lrt.rtemsrwset.*)))
  }  
  .lrt.data :
  {
    lrt_data_start = .;
    lrt__data_start = .;
    lrt__sdata = . ;
    *(.lrt.data*)
    *(.lrt.gnu.linkonce.d*)
    . = ALIGN(0x10);
    *(.lrt.gcc_except_table*)
    . = ALIGN(0x10);
    lrt_edata = .;
    lrt__edata = .;
  }  
  
  /*The LeonRT default heap section*/
  .lrt.heapSection : { *(.lrt.heapSection.data) }
  
  
  .lrt.dynamic	  : { *(.lrt.dynamic) 	}  
  .lrt.jcr		  : { *(.lrt.jcr) 		}  
  .lrt.got		  : { *(.lrt.got) 		}  
  .lrt.plt   	  : { *(.lrt.plt) 		}  
  .lrt.hash		  : { *(.lrt.hash)		}  
  .lrt.dynrel	  : { *(.lrt.dynrel)	}  
  .lrt.dynsym	  : { *(.lrt.dynsym)	}  
  .lrt.dynstr	  : { *(.lrt.dynstr)	}  
  .lrt.hash		  : { *(.lrt.hash)		}  
  .lrt.shbss :
  {
    *(.lrt.shbss)
  }  
  
  .lrt.bss :
  {
    lrt___bss_start = ALIGN(0x8);
    lrt__bss_start = .;
    lrt_bss_start = .;
    *(.lrt.bss .lrt.bss* .lrt.gnu.linkonce.b*)
    *(.lrtCOMMON)
    . = ALIGN (16);
    lrt_end = .;
    lrt__end = ALIGN(0x8);
    lrt___end = ALIGN(0x8);
  }  
  
  .lrt.noinit (NOLOAD) : { 
    . = ALIGN(8);
    lrt___noinit_start = . ;
    *(.lrt.noinit*) *(.lrtCOMMON)
    . = ALIGN(8);
    lrt___noinit_end = . ;
  }
  
  /*Move section pointer to the end of the allocated memory*/
  . = lrt___sys_bss_start;
  lrt__LEON_STACK_POINTER = .;
  .lrt.sys.bss ALIGN(4) (NOLOAD) : { 
    . = ALIGN(8);
    lrt___sys_bss_start = . ;
    *(.lrt.sys.bss*) 
    . = ALIGN(8);
    lrt___sys_bss_end = . ;
  }
  
  .lrt.stab . (NOLOAD) :
  {
    [ .lrt.stab ]
  }
  .lrt.stabstr . (NOLOAD) :
  {
    [ .lrt.stabstr ]
  }
  
 
  __MIN_STACK_SIZE = DEFINED( __MIN_STACK_SIZE ) ? __MIN_STACK_SIZE : 3584;
  __MIN_INIT_STACK_SIZE = DEFINED( __MIN_INIT_STACK_SIZE ) ? __MIN_INIT_STACK_SIZE : 1024;
  
  __STACK_SIZE = ABSOLUTE(__sys_bss_start - __bss_end);
  ASSERT( (__STACK_SIZE > __MIN_STACK_SIZE) && (__STACK_SIZE < 0x80000000) , "LeonOS Code + data too big. Stack space left is smaller than the required minimum (3.5 KB)!")

  lrt__MIN_STACK_SIZE = DEFINED( lrt__MIN_STACK_SIZE ) ? lrt__MIN_STACK_SIZE : 3584;
  /*lrt__MIN_INIT_STACK_SIZE = DEFINED( lrt__MIN_INIT_STACK_SIZE ) ? lrt__MIN_INIT_STACK_SIZE : 1024;*/
  
  lrt__STACK_SIZE = ABSOLUTE(lrt___sys_bss_start - lrt___bss_end);
  ASSERT( (lrt__STACK_SIZE > lrt__MIN_STACK_SIZE) && (lrt__STACK_SIZE < 0x80000000) , "LeonRT Code + data too big. Stack space left is smaller than the required minimum (3.5 KB)!")
  
  /DISCARD/ : {
    *(.comment .gnu.attributes .iplt .rela.iplt)
  }
}
